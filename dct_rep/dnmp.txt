// DNMP command/reply trust schema

// this network's namespace 
_network: "AliceNetOps"

// the trust anchor cert name
netCert:  _network/_keyinfo

// subdomain associated with this schema & associated cert 
_domain: "dnmp"
dnmpCert: _network/_domain/_keyinfo <= netCert

// command publication definition

#command: _network/_domain/target/_topic/_roleId/pType/pArgs/_origin/_cTS &
                { _topic: "command", _origin: sysId(), _cTS: timestamp() }

// Operators can issue any command
ocommand: #command <= opCert

// Users can issue any command to local nod and ping commands to any nod
ucommand: #command & { target: "local" } | { pType: "Pinger" } <= userCert

// Certificates and signing chain for roles that can issue commands

roleCert: _network/_domain/_role/_roleId/_keyinfo <= dnmpCert
opCert:   roleCert & { _role: "operator" }
userCert: roleCert & { _role: "user" }

// reply publication definition

nodCert:    _network/_domain/"nod"/_nodId/_keyinfo <= deviceCert
deviceCert: _network/"device"/devId/_keyinfo <= configCert
configCert: _network/"config"/confId/_keyinfo <= netCert

// Prefix used at the NDN Interest/Data level to sync this collection.
#wirePrefix: _ndnprefix/_domain/target
wpLocal: #wirePrefix & { _ndnprefix: "localhost", target: "local" }
wpOther: #wirePrefix & { _ndnprefix: "localnet" }

// NDN certificate format v2 suffix for a cert name. Final 3 components
// are keyID, issuerID and version. The key management system uses these
// but not this schema so they're left anonymous here.
_keyinfo: "KEY"
